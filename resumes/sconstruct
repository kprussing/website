#!/usr/bin/env python 
"""The script to build my resumes."""

import os

env = Environment(ENV=os.environ, tools=["default", "pandoc"])

Export("env")
pubs = SConscript(os.path.join("publications", "SConscript"))

srcs = ["index.md", "experience.md", "awards.md"] + pubs \
    + [x + ".md" for x in
       ("projects", "skills", "education", "old-work", "footer")]

css = " ".join(["--css={0}.css".format(x)
                for x in ("website-colors", "style")])

env.Pandoc("full.html", srcs,
           PANDOCFLAGS="--self-contained " + css)
env.Pandoc("short.html", "short.text",
           PANDOCFLAGS="--self-contained --css=short.css")

if not GetOption("clean"):
    Default("full.html")

